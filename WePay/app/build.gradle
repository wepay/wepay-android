apply plugin: 'com.android.library'
group= 'com.wepay.android'

android {
    compileSdkVersion 23
    buildToolsVersion '24.0.2'
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 23
        versionCode 1
        versionName '3.0.0-beta-1'
        project.archivesBaseName = "wepay-android"
        project.version = android.defaultConfig.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            testCoverageEnabled = true
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}.aar"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.7.201606060606"
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

configurations {
    javadocDeps
}

dependencies {
    compile 'com.android.support:support-v4:23.2.0'
    compile 'com.google.code.gson:gson:2.6.2'

    compile files('libs/android-async-http-1.4.5.jar')
    compile fileTree(dir: 'libs/internal', include: ['*.jar'])
    androidTestCompile 'com.android.support.test:runner:0.5'

    javadocDeps 'com.google.code.gson:gson:2.6.2'
    javadocDeps files('libs/android-async-http-1.4.5.jar')

    javadocDeps fileTree(dir: 'libs/internal', include: ['*.jar'])
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
    classpath += configurations.javadocDeps
    exclude "**/internal/**"
    //options.overview = "$rootProject.projectDir/../Documentation/html/readme.html"
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateSourcesJar
    archives generateJavadocsJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    filesSpec {
        from '../../WePayExample/app/libs/'
        into 'com/wepay/android/wepay-android/'+android.defaultConfig.versionName
    }

    pkg {
        repo = 'maven'
        name = 'wepay-android'
        userOrg = 'wepay'
        vcsUrl = 'https://github.com/wepay/wepay-android.git'

        version {
            name = android.defaultConfig.versionName
            released  = new Date()
            vcsTag = android.defaultConfig.versionName
        }

        licenses = ['Apache-2.0']
    }
}
